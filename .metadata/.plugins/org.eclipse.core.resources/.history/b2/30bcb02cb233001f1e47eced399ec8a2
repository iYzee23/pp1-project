package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("Syntax error", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatal error, parsing couldn't be continued", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" on line ").append(((Symbol)info).left);
        log.error(msg.toString());
    }

:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}



/* Terminals */

terminal PROG, LBRACE, RBRACE, NAMESP, COMMA, CONST, SEMI, ASSIGN, LBRACKET, RBRACKET, CLASS, EXTENDS, STATIC;
terminal LPAREN, RPAREN, VOID, RESOLUTION, INC, DEC, EQUAL, NOT_EQUAL, GRT, GRT_EQUAL, LESS, LESS_EQUAL, PRINT;
terminal PLUS, MINUS, MUL, DIV, MOD, DOT, OR, AND, NEW, RANGE, FOR, IN, IF, ELSE, BREAK, CONTINUE, RETURN, READ; 
terminal String IDENT;
terminal Integer NUM_CONST;
terminal Boolean BOOL_CONST;
terminal Character CHAR_CONST;



/* Non-terminals */

nonterminal Program, NamespaceList, DeclList, MethodDeclList, Namespace, ConstDecl, VarDecl, ClassDecl, MethodDecl;
nonterminal ConstParts, ConstPart, Type, ConstChoice, VarParts, VarPart, StatVarDeclList;
nonterminal StatInitList, VarDeclList, StaticInitializer, StatementList, Statement, ReturnType;
nonterminal FormPars, DesignatorStatement, Designator, OpChoice, DesignatorList;
nonterminal ActPars, Label, Assignop, Relop, Addop, Mulop, DesignatorParts;
nonterminal Condition, CondTerm, CondFact, Expr, Term, Factor, CondTermList, CondFactList, RelopExprOpt, MinusOpt; 
nonterminal AddopTermList, MulopFactorList, ParensOpt, NewChoice, IfOpt, ElseOpt, ExprOpt, NumConstOpt, CondFactOpt;
nonterminal DesignatorStmtList, DesignatorStmtOpt;



/* Precedences */

precedence left ELSE;



/* Grammar */

Program ::= (Program) PROG IDENT NamespaceList DeclList LBRACE MethodDeclList RBRACE;

NamespaceList ::= (NamespaceListYes) NamespaceList Namespace
			   |
			   (NamespaceListNo) /* epsilon */;

DeclList ::= (DeclListConst) DeclList ConstDecl
		  |
		  (DeclListVar) DeclList VarDecl
		  |
		  (DeclListClass) DeclList ClassDecl
		  |
		  (DeclListNo) /* epsilon */;

MethodDeclList ::= (MethodDeclListYes) MethodDeclList MethodDecl
				|
				(MethodDeclListNo) /* epsilon */;

Namespace ::= NAMESP IDENT LBRACE DeclList LBRACE MethodDeclList RBRACE RBRACE;

ConstDecl ::= (ConstDecl) ConstParts SEMI;

ConstParts ::= (ConstDeclList) ConstParts COMMA ConstPart
		    |
		    (ConstDeclSingle) CONST Type ConstPart;

ConstPart ::= (ConstPart) IDENT ASSIGN ConstChoice;

ConstChoice ::= (ConstNum) NUM_CONST
			 |
			 (ConstChar) CHAR_CONST
			 |
			 (ConstBool) BOOL_CONST;

VarDecl ::= (VarDecl) VarParts SEMI;

VarParts ::= (VarDeclList) VarParts COMMA VarPart
		  |
		  (VarDeclSingle) Type VarPart;

VarPart ::= (VarPartYes) IDENT LBRACKET RBRACKET
		 |
		 (VarPartNo) IDENT;

ClassDecl ::= (ClassDeclYesYes) CLASS IDENT EXTENDS Type LBRACE StatVarDeclList StatInitList VarDeclList LBRACE MethodDeclList RBRACE RBRACE
		   |
		   (ClassDeclYesNo) CLASS IDENT EXTENDS Type LBRACE StatVarDeclList StatInitList VarDeclList RBRACE
		   |
		   (ClassDeclNoYes) CLASS IDENT LBRACE StatVarDeclList StatInitList VarDeclList LBRACE MethodDeclList RBRACE RBRACE;
		   |
		   (ClassDeclNoNo) CLASS IDENT LBRACE StatVarDeclList StatInitList VarDeclList RBRACE;

StatVarDeclList ::= (StatVarDeclListYes) StatVarDeclList STATIC VarDecl
				 |
				 (StatVarDeclListNo) /* epsilon */;

StatInitList ::= (StatInitListYes) StatInitList StaticInitializer
			  |
			  (StatInitListNo) /* epsilon */;

VarDeclList ::= (VarDeclListYes) VarDeclList VarDecl
			 |
			 (VarDeclListNo) /* epsilon */;

StaticInitializer ::= (StaticInitializer) STATIC LBRACE StatementList RBRACE;

StatementList ::= (StatementListYes) StatementList Statement
			   |
			   (StatementListNo) /* epsilon */; 

MethodDecl ::= (MethodDeclYes) ReturnType IDENT LPAREN FormPars RPAREN VarDeclList LBRACE StatementList RBRACE
			|
			(MethodDeclNo) ReturnType IDENT LPAREN RPAREN VarDeclList LBRACE StatementList RBRACE;

ReturnType ::= (ReturnTypeType) Type
			|
			(ReturnTypeVoid) VOID;
			 
FormPars ::= (FormParsListYes) FormPars COMMA Type IDENT LBRACKET RBRACKET 
		  |
		  (FormParsListNo) FormPars COMMA Type IDENT
		  |
		  (FormParsSingleYes) Type IDENT LBRACKET RBRACKET
		  |
		  (FormParsSingleNo) Type IDENT;

Type ::= (TypeYes) IDENT RESOLUTION IDENT
	  |
	  (TypeNo) IDENT;

Statement ::= (StmtDesignator) DesignatorStatement SEMI
		   |
		   (StmtIfElse) IF LPAREN Condition RPAREN Statement ElseOpt
		   |
		   (StmtBreak) BREAK SEMI
		   |
		   (StmtContinue) CONTINUE SEMI
		   |
		   (StmtReturn) RETURN ExprOpt SEMI
		   |
		   (StmtRead) READ LPAREN Designator RPAREN SEMI
		   |
		   (StmtPrint) PRINT LPAREN Expr NumConstOpt RPAREN SEMI
		   |
		   (StmtFor) FOR LPAREN DesignatorStmtOpt SEMI CondFactOpt SEMI DesignatorStmtOpt RPAREN Statement
		   |
		   (StmtBlock) LBRACE StatementList RBRACE;
		   
ElseOpt ::= (ElseOptYes) ELSE Statement
		 |
		 (ElseOptNo) /* epsilon */;

ExprOpt ::= (ExprOptYes) Expr
		 |
		 (ExprOptNo) /* epsilon */;

NumConstOpt ::= (NumConstOptYes) COMMA NUM_CONST
			 |
			 (NumConstOptNo) /* epsilon */;
			 
CondFactOpt ::= (CondFactOptYes) CondFact
			 |
			 (CondFactOptNo) /* epsilon */;

DesignatorStmtOpt ::= (DesignatorStmtOptYes) DesignatorStatement DesignatorStmtList
				   |
				   (DesignatorStmtOptNo) /* epsilon */;
				   
DesignatorStmtList ::= (DesignatorStmtListYes) DesignatorStmtList COMMA DesignatorStatement
					|
					(DesignatorStmtListNo) /* epsilon */;

DesignatorStatement ::= (DesignatorStmtFirst) Designator OpChoice
					 |
					 (DesignatorStmtSecond) LBRACKET DesignatorList MUL Designator RBRACKET ASSIGN Designator
					 |
					 (DesignatorStmtThird) Designator ASSIGN LBRACKET FOR Expr IN Designator IfOpt RBRACKET;

IfOpt ::= (IfOptYes) IF Condition
	   |
	   (IfOptNo) /* epsilon */;
					 
OpChoice ::= (OpChoiceExpr) Assignop Expr
		  |
		  (OpChoiceActParsYes) LPAREN ActPars RPAREN
		  |
		  (OpChoiceActParsNo) LPAREN RPAREN
		  |
		  (OpChoiceInc) INC
		  |
		  (OpChoiceDec) DEC;

DesignatorList ::= (DesignatorListYesYes) DesignatorList Designator COMMA
				|
				(DesignatorListYesNo) DesignatorList COMMA
				|
				(DesignatorListNo) /* epsilon */;

ActPars ::= (ActParsList) ActPars COMMA Expr
		 |
		 (ActParsSingle) Expr;

Condition ::= (Condition) CondTerm CondTermList;

CondTermList ::= (CondTermListYes) CondTermList OR CondTerm
			  |
			  (CondTermListNo) /* epsilon */;

CondTerm ::= (CondTerm) CondFact CondFactList;

CondFactList ::= (CondFactListYes) CondFactList AND CondFact
			  |
			  (CondFactListNo) /* epsilon */;

CondFact ::= (CondFactYes) Expr RelopExprOpt
		  |
		  (CondFactNo) Expr;

RelopExprOpt ::= (RelopExprOptYes) Relop Expr
			  |
			  (RelopExprOptNo) /* epsilon */;

Expr ::= (Expr) MinusOpt Term AddopTermList;

MinusOpt ::= (MinusOptYes) MINUS
		  |
		  (MinusOptNo) /* epsilon */;

AddopTermList ::= (AddopTermListYes) AddopTermList Addop Term
			   |
			   (AddopTermListNo) /* epsilon */;

Term ::= (Term) Factor MulopFactorList;

MulopFactorList ::= (MulopFactorListYes) MulopFactorList Mulop Factor
				 |
				 (MulopFactorListNo) /* epsilon */;

Factor ::= (FactorDesignator) Designator ParensOpt
		|
		(FactorNum) NUM_CONST
		|
		(FactorChar) CHAR_CONST
		|
		(FactorBool) BOOL_CONST
		|
		(FactorNew) NEW Type NewChoice
		|
		(FactorExpr) LPAREN Expr RPAREN
		|
		(FactorRange) RANGE LPAREN Expr RPAREN;
		
ParensOpt ::= (ParensOptYesYes) LPAREN ActPars RPAREN
		   |
		   (ParensOptYesNo) LPAREN RPAREN
		   |
		   (ParensOptNo) /* epsilon */;

NewChoice ::= (NewChoiceExpr) LBRACKET Expr RBRACKET
		   |
		   (NewChoiceActParsYes) LPAREN ActPars RPAREN
		   |
		   (NewChoiceActParsNo) LPAREN RPAREN
		   ;

Designator ::= (DesignatorYes) IDENT RESOLUTION IDENT DesignatorParts
			|
			(DesignatorNo) IDENT DesignatorParts;

DesignatorParts ::= (DesignatorPartsIdent) DesignatorParts DOT IDENT
				 |
				 (DesignatorPartsExpr) DesignatorParts LBRACKET Expr RBRACKET
				 |
				 (DesignatorPartsNo) /* epsilon */;

Label ::= (Label) IDENT;

Assignop ::= (AssignopAssign) ASSIGN;

Relop ::= (RelopEqual) EQUAL
	   |
	   (RelopNotEqual) NOT_EQUAL
	   |
	   (RelopGrt) GRT
	   |
	   (RelopGrtEqual) GRT_EQUAL
	   |
	   (RelopLess) LESS
	   |
	   (RelopLessEqual) LESS_EQUAL;

Addop ::= (AddopPlus) PLUS
	   |
	   (AddopMinus) MINUS;

Mulop ::= (MulopMul) MUL
	   |
	   (MulopDiv) DIV
	   |
	   (MulopMod) MOD;
